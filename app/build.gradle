buildscript {
    ext.kotlin_version = '1.3.30'
    ext.acraVersion = '5.1.3'
    ext.retrofitVersion = '2.5.0'
    ext.okhttpVersion = '3.14.0'

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.gladed.androidgitversion' version '0.4.7'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

androidGitVersion {
    codeFormat 'MNNPPPPPP'
    format '%tag%'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }

    defaultConfig {
        applicationId "org.blokada"
        minSdkVersion 21
        targetSdkVersion 28
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86'
        }
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        // Set the BLOKADA_* constants in your ~/.gradle/gradle.properties to point to your keystore.
        release {
            keyAlias 'blokada'
            keyPassword BLOKADA_KEY_PASSWORD
            storeFile file(BLOKADA_KEY_PATH)
            storePassword BLOKADA_STORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            // By default it is a 'debuggable' build, signed with a debug key.
        }

        release {
            /**
             * Release builds are published on F-Droid and XDA Labs. They adhere to F-Droid
             * inclusion standards, which means they cannot contain any non-free software or
             * tracking, hence we can provide only limited support for them.
              */
            minifyEnabled false
            useProguard false // We don't use Proguard for now.
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        official {
            /**
             * Official builds are published on blokada.org and Google Play. They contain optional
             * reporting so we can provide better support and troubleshooting.
             */
            initWith(release)
            matchingFallbacks = ['release']
        }

        beta {
            // 'Beta' are builds used for internal and external beta testing.
            initWith(release)
            matchingFallbacks = ['release']
        }
    }

    // It needs to be defined in order for the flavor setup to work.
    flavorDimensions("model")

    productFlavors {
        appFull {
            // appFull lands on blokada.org.
            applicationId "org.blokada.origin.alarm"
        }

        appCommunity {
            // appCommunity lands on F-Droid and XDA Labs.
            applicationId "org.blokada.alarm"
        }

        appGoogle {
            // appGoogle build lands on Google Play.
            applicationId "org.blokada.alarm.dnschanger"
        }

        /**
         * You can choose any of those modules in 'Build Variants' in Android Studio to work only on
         * selected modules or run only relevant test suites. You cannot actually run those modules
         * as apps. It is meant to help improve code separation as well as speed up development.
         */

        modCommon {
            // modCommon is included by all other Kotlin mods.
        }

        modG11n {
            /**
             * modG11n (Globalization) provides all i18n (internationalization) and l10n
             * (localization) functionality on top of what Android SDK provides.
              */
        }

        modWireguard {
            // modWireguard provides Wireguard client native library and JNI bindings.
        }

        modTunnel {
            /**
             * Tunnel mod implements TUN/TAP tunneling (either with Wireguard or fake VPN) in
             * Kotlin. It also provides hosts lists filtering and has too much responsibility.
              */
        }

        modLegacy {
            // A catch all that is going away.
        }
    }

    sourceSets {
        def srcCommonKotlin = 'src/modCommon/kotlin'
        def srcCommonMock = 'src/modCommon/mock'
        def srcCommonTest = 'src/modCommon/test'

        def srcWireguardKotlin = 'src/modWireguard/kotlin'
        def srcWireguardJni = 'src/modWireguard/jniLibs'

        def srcG11nKotlin = 'src/modG11n/kotlin'
        def srcG11nTest = 'src/modG11n/test'

        def srcTunnelKotlin = 'src/modTunnel/kotlin'
        def srcTunnelTest = 'src/modTunnel/test'

        def srcLegacyKotlin = 'src/modLegacy/kotlin'
        def srcLegacyRes = 'src/modLegacy/res'
        def srcLegacyTest = 'src/modLegacy/test'

        modCommon.java.srcDirs += srcCommonKotlin
        testModCommon.java.srcDirs += srcCommonTest

        appFull.manifest.srcFile 'src/modLegacy/AndroidManifest.xml'
        appFull.java.srcDirs += srcCommonKotlin
        appFull.java.srcDirs += srcTunnelKotlin
        appFull.java.srcDirs += srcG11nKotlin
        appFull.java.srcDirs += srcWireguardKotlin
        appFull.java.srcDirs += srcLegacyKotlin
        appFull.java.srcDirs += 'src/appFull/kotlin'
        appFull.jniLibs.srcDirs += srcWireguardJni
        appFull.res.srcDirs += srcLegacyRes
        appFull.res.srcDirs += 'src/appFull/res'
//        testAppFull.java.srcDirs += testModTunnel.java.srcDirs
//        testAppFull.java.srcDirs += testModG11n.java.srcDirs
//        testAppFull.java.srcDirs += testModWireguard.java.srcDirs

        appCommunity.manifest.srcFile 'src/modLegacy/AndroidManifest.xml'
        appCommunity.java.srcDirs += srcCommonKotlin
        appCommunity.java.srcDirs += srcTunnelKotlin
        appCommunity.java.srcDirs += srcG11nKotlin
        appCommunity.java.srcDirs += srcWireguardKotlin
        appCommunity.java.srcDirs += srcLegacyKotlin
        appCommunity.java.srcDirs += 'src/appFull/kotlin'
        appCommunity.jniLibs.srcDirs += srcWireguardJni
        appCommunity.res.srcDirs += srcLegacyRes
        appCommunity.res.srcDirs += 'src/appFull/res'

        appGoogle.manifest.srcFile 'src/modLegacy/AndroidManifest.xml'
        appGoogle.java.srcDirs += srcCommonKotlin
        appGoogle.java.srcDirs += srcTunnelKotlin
        appGoogle.java.srcDirs += srcG11nKotlin
        appGoogle.java.srcDirs += srcWireguardKotlin
        appGoogle.java.srcDirs += srcLegacyKotlin
        appGoogle.java.srcDirs += 'src/appGoogle/kotlin'
        appGoogle.jniLibs.srcDirs += srcWireguardJni
        appGoogle.res.srcDirs += srcLegacyRes
        appGoogle.res.srcDirs += 'src/appGoogle/res'

        /**
         * Build types are cross combined with flavors to make a final release
          */

        debug.java.srcDirs += 'src/buildUnofficial/kotlin'
        release.java.srcDirs += 'src/buildOfficial/kotlin'
        release.res.srcDirs += 'src/buildOfficial/res'
        official.java.srcDirs += 'src/buildOfficial/kotlin'
        official.res.srcDirs += 'src/buildOfficial/res'
        beta.java.srcDirs += 'src/buildOfficial/kotlin'
        beta.res.srcDirs += 'src/buildOfficial/res'
    }
}

dependencies {
    // Android support libraries
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    // Kotlin and its dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'nl.komponents.kovenant:kovenant-core:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-android:3.3.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.23.1"
//    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-M1'
    implementation 'io.paperdb:paperdb:2.6'
    implementation 'com.github.michaelbull:kotlin-result:1.0.8'

    // Proxy
    implementation 'org.pcap4j:pcap4j-core:1.7.0'
    implementation 'org.pcap4j:pcap4j-packetfactory-propertiesbased:1.7.0'
    implementation 'dnsjava:dnsjava:2.1.8'
    implementation 'org.slf4j:slf4j-nop:1.6.1'

    // UI libraries
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.ramotion.foldingcell:folding-cell:1.2.2'
    implementation 'jp.wasabeef:blurry:3.0.0'
    implementation 'com.github.ps100000:PageIndicatorView:v1.0.4'

    // REST api
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation ("com.squareup.retrofit2:retrofit:$retrofitVersion"){
        exclude module: 'okhttp'
    }
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Testing and debugging
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
